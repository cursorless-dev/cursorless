;; import python.fieldAccess.scm

;; Generated by the following command:
;; > curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-python/d6210ceab11e8d812d4ab59c07c81458ec6e5184/src/node-types.json \
;;   | jq '[.[] | select(.type == "_simple_statement" or .type == "_compound_statement") | .subtypes[].type]'
[
  (assert_statement)
  (break_statement)
  (class_definition)
  (continue_statement)
  (decorated_definition)
  (delete_statement)
  (exec_statement)
  (expression_statement)
  (for_statement)
  (function_definition)
  (future_import_statement)
  (global_statement)
  (if_statement)
  (import_from_statement)
  (import_statement)
  (match_statement)
  (nonlocal_statement)
  (pass_statement)
  (print_statement)
  (raise_statement)
  (return_statement)
  (try_statement)
  (while_statement)
  (with_statement)
] @statement

;;!! a = 25
;;!      ^^
;;!   xxxxx
;;!  ------
(assignment
  (_) @_.leading.start.endOf
  .
  right: (_) @value @_.leading.end.startOf
) @_.domain

;; value:
;;!! a /= 25
;;!       ^^
;;!   xxxxxx
;;!  -------
;; name:
;;!! a /= 25
;;!  ^
;;!  xxxxx
;;!  -------
(augmented_assignment
  left: (_) @name @name.trailing.start.endOf @value.leading.start.endOf
  right: (_) @value @value.leading.end.startOf @name.trailing.end.startOf
) @_.domain

;;!! a = 25
;;!  ^
;;!  xxxx
;;!  ------
;;!! a: int = 25
;;!  ^
;;!  xxxxxxxxx
;;!  -----------
(assignment
  left: (_) @name @name.trailing.start.endOf
  right: (_)? @name.trailing.end.startOf
) @_.domain

(_
  name: (_) @name
) @_.domain

;;!! def aaa(bbb):
;;!          ^^^
(parameters
  (identifier) @name
)

;;!! def aaa(bbb: str):
;;!          ^^^
;;!          --------
(typed_parameter
  .
  (_) @name
) @_.domain

;; Matches any node at field `type` of its parent, with leading delimiter until
;; previous named node. For example:
;;!! aaa: str = "bbb";
;;!       ^^^
;;!  -----------------
;;!     xxxxx
(_
  (_) @_.leading.start.endOf
  .
  type: (_) @type @_.leading.end.startOf
) @_.domain

;;!!  def aaa() -> str:
;;!                ^^^
;;!            xxxxxxx
;;!  [-----------------
;;!!      pass
;;!   --------]
(function_definition
  (_) @_.leading.start.endOf
  .
  return_type: (_) @type @_.leading.end.startOf
) @_.domain

;;!! d = {"a": 1234}
;;!            ^^^^
;;!          xxxxxx
;;!       ---------
;;!! {value: key for (key, value) in d1.items()}
;;!          ^^^
;;!        xxxxx
;;!   ----------
;;!! def func(value: str = ""):
;;!                        ^^
;;!                     xxxxx
;;!           ---------------
(
  (_
    (_) @_.leading.start.endOf
    .
    value: (_) @value @_.leading.end.startOf
  ) @_.domain
  (#not-type? @_.domain subscript)
)

;;!! return 1
;;!         ^
;;!        xx
;;!  --------
;;
;; NOTE: in tree-sitter, both "return" and the "1" are children of `return_statement`
;; but "return" is anonymous whereas "1" is named node, so no need to exclude explicitly
(return_statement
  (_) @value
) @_.domain

;; value:
;;!! for aaa in bbb:
;;!             ^^^
;;!  ---------------
;; name:
;;!! for aaa in bbb:
;;!      ^^^
;;!  ---------------
(for_statement
  left: (_) @name
  right: (_) @value
  ":" @_.domain.end
) @_.domain.start.startOf

(comment) @comment @textFragment

(string
  _ @textFragment.start.endOf
  _ @textFragment.end.startOf
) @string

[
  (dictionary)
  (dictionary_comprehension)
] @map

[
  (list)
  (list_comprehension)
  (set)
] @list

(
  (function_definition
    name: (_) @functionName
    body: (_) @namedFunction.interior
  ) @namedFunction @functionName.domain
  (#not-parent-type? @namedFunction decorated_definition)
)
(decorated_definition
  (function_definition
    name: (_) @functionName
    body: (_) @namedFunction.interior
  )
) @namedFunction @functionName.domain

(
  (class_definition
    name: (_) @className
    body: (_) @class.interior
  ) @class @className.domain
  (#not-parent-type? @class decorated_definition)
)
(decorated_definition
  (class_definition
    name: (_) @className
    body: (_) @class.interior
  )
) @class @className.domain

(module) @className.iteration @class.iteration
(module) @statement.iteration @value.iteration @name.iteration
(module) @namedFunction.iteration @functionName.iteration
(class_definition) @namedFunction.iteration @functionName.iteration

;;!! def foo():
;;!!     a = 0
;;!     <*****
;;!!     b = 1
;;!      *****
;;!!     c = 2
;;!      *****>
(block) @statement.iteration @value.iteration @name.iteration
(block) @type.iteration

;;!! {"a": 1, "b": 2, "c": 3}
;;!   **********************
(dictionary
  "{" @value.iteration.start.endOf
  "}" @value.iteration.end.startOf
)

;;!! def func(a=0, b=1):
;;!           ********
(parameters
  "(" @value.iteration.start.endOf @name.iteration.start.endOf @type.iteration.start.endOf
  ")" @value.iteration.end.startOf @name.iteration.end.startOf @type.iteration.end.startOf
)

;;!! if true: pass
;;!  ^^^^^^^^^^^^^
(if_statement) @ifStatement

;;!! foo()
;;!  ^^^^^
(call) @functionCall

;;!! foo()
;;!  ^^^^^
(call
  function: (_) @functionCallee
) @_.domain

;;!! lambda _: pass
;;!  ^^^^^^^^^^^^^^
(lambda) @anonymousFunction

;;!! match value:
;;!        ^^^^^
(match_statement
  subject: (_) @switchStatementSubject
) @_.domain

;;!! { "value": 0 }
;;!    ^^^^^^^
;;!    xxxxxxxxx
(pair
  key: (_) @collectionKey @collectionKey.trailing.start.endOf
  value: (_) @collectionKey.trailing.end.startOf
) @_.domain

;;!! if True:
;;!     ^^^^
(if_statement
  condition: (_) @condition
) @_.domain

;;!! elif True:
;;!     ^^^^
(elif_clause
  condition: (_) @condition
) @_.domain

;;!! while True:
;;!        ^^^^
(while_statement
  condition: (_) @condition
) @_.domain

;;!! match value:
;;!        ^^^^^
(case_clause
  pattern: (_) @condition
) @_.domain

;;!! case a if a > 1:
;;!            ^^^^^
;;!         xxxxxxxx
;;!         --------
(case_clause
  (if_clause
    "if"
    (_) @condition
  ) @_.domain @_.removal
)

;;!! case 0: pass
;;!  ^^^^^^^^^^^^
(case_clause) @branch

(match_statement) @branch.iteration @condition.iteration

;;!! 1 if True else 0
;;!       ^^^^
;;!  ----------------
(
  (conditional_expression
    "if"
    .
    (_) @condition
  ) @_.domain
)

;;!! 1 if True else 0
;;!  ^
(
  (conditional_expression
    (_) @branch
    .
    "if"
  )
)

;;!! 1 if True else 0
;;!                 ^
(
  (conditional_expression
    "else"
    .
    (_) @branch
  )
)

(conditional_expression) @branch.iteration

;;!! [aaa for aaa in bbb if ccc]
;;!                         ^^^
;;!                      xxxxxx
;;!  ---------------------------
(list_comprehension
  (if_clause
    "if"
    (_) @condition
  ) @_.removal
) @_.domain

;;!! (aaa for aaa in bbb if ccc)
;;!                         ^^^
;;!                      xxxxxx
;;!  ---------------------------
(generator_expression
  (if_clause
    "if"
    (_) @condition
  ) @_.removal
) @_.domain

;;!! {aaa for aaa in bbb if ccc}
;;!                         ^^^
;;!                      xxxxxx
;;!  ---------------------------
(set_comprehension
  (if_clause
    "if"
    (_) @condition
  ) @_.removal
) @_.domain

;;!! {aaa: aaa for aaa in bbb if ccc}
;;!                              ^^^
;;!                           xxxxxx
;;!  --------------------------------
(dictionary_comprehension
  (if_clause
    "if"
    (_) @condition
  ) @_.removal
) @_.domain

;;!! for name in value:
;;!      ^^^^    ^^^^^
;;!  ------------------
(for_statement
  left: (_) @name
  right: (_) @value
) @_.domain

;;!! if True: pass
;;!  ^^^^^^^^^^^^^
(if_statement
  "if" @branch.start
  consequence: (_) @branch.end
)

;;!! elif True: pass
;;!  ^^^^^^^^^^^^^^^
(elif_clause) @branch

;;!! else: pass
;;!  ^^^^^^^^^^
(else_clause) @branch

(if_statement) @branch.iteration

;;!! try: pass
;;!  ^^^^^^^^^
(try_statement
  "try" @branch.start
  body: (_) @branch.end
)

;;!! except: pass
;;!  ^^^^^^^^^^^^
(except_clause) @branch

;;!! finally: pass
;;!  ^^^^^^^^^^^^^
(finally_clause) @branch

(try_statement) @branch.iteration

;;!! while True: pass
;;!  ^^^^^^^^^^^^^^^^
(while_statement
  "while" @branch.start
  body: (_) @branch.end
)

(while_statement) @branch.iteration

;;!! for aaa in bbb: pass
;;!  ^^^^^^^^^^^^^^^^^^^^
(for_statement
  "for" @branch.start
  body: (_) @branch.end
)

(for_statement) @branch.iteration
