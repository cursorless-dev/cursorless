;; https://github.com/tree-sitter/tree-sitter-rust/blob/master/src/grammar.json

;; Generated by the following command:
;; `curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-rust/36ae187ed6dd3803a8a89dbb54f3124c8ee74662/src/node-types.STATEMENT_TYPES | jq '[.[] | select(.type == "_declaration_statement") | .subtypes[].type, "expression_statement"]'`
(
  (_
    [
      (associated_type)
      (attribute_item)
      (const_item)
      (empty_statement)
      (extern_crate_declaration)
      (foreign_mod_item)
      (inner_attribute_item)
      (let_declaration)
      (macro_definition)
      (macro_invocation)
      (function_item)
      (function_signature_item)
      (mod_item)
      (static_item)
      (trait_item)
      (struct_item)
      (impl_item)
      (enum_item)
      (type_item)
      (union_item)
      (use_declaration)
      (expression_statement)
    ] @statement
  ) @_dummy
  (#type? @_dummy source_file block declaration_list)
)

(
  (source_file) @statement.iteration @class.iteration @namedFunction.iteration
  (#document-range! @statement.iteration @class.iteration @namedFunction.iteration)
)
(
  (source_file) @name.iteration @type.iteration @value.iteration
  (#document-range! @name.iteration @value.iteration @type.iteration)
)

;;!! { }
;;!   ^
(_
  .
  "{" @interior.start.endOf @statement.iteration.start.endOf
  "}" @interior.end.startOf @statement.iteration.end.startOf
  .
)
(_
  .
  "{" @name.iteration.start.endOf @value.iteration.start.endOf @type.iteration.start.endOf
  "}" @name.iteration.end.startOf @value.iteration.end.startOf @type.iteration.end.startOf
  .
)

;;!! if v < 0 {}
;;!  ^^^^^^^^^^^
(
  (if_expression) @ifStatement @statement @branch.iteration
  (#not-parent-type? @ifStatement else_clause)
)

;;!! if let Some(i) = number {}
;;!  ^^^^^^^^^^^^^^^^^^^^^^^^^^
;;!         ^^^^^^^^^^^^^^^^
(if_let_expression
  "let" @condition.start
  value: (_) @condition.end
) @ifStatement @_.domain

;;!! if true {}
;;!  ^^^^^^^^^^
(_
  (if_expression
    "if" @branch.start @branch.removal.start
    condition: (_) @condition
    consequence: (_) @branch.end @branch.removal.end
    alternative: (else_clause
      (if_expression) @branch.removal.end.startOf
    )?
  ) @condition.domain
  (#not-parent-type? @condition.domain else_clause)
)

;;!! else if true {}
;;!  ^^^^^^^^^^^^^^^
(else_clause
  "else" @branch.start @condition.domain.start
  (if_expression
    condition: (_) @condition
    consequence: (_) @branch.end @condition.domain.end
  )
)

;;!! else {}
;;!  ^^^^^^^
(
  (else_clause
    (block)
  ) @branch
)

;;!! "hello"
(
  (string_literal) @string @textFragment
  (#child-range! @textFragment 0 -1 true true)
)

;;!! r#"foobar"#
(
  (raw_string_literal) @string @textFragment
  (#shrink-to-match! @textFragment "r#+\"(?<keep>.*)\"#+")
)

[
  (line_comment)
  (block_comment)
] @comment @textFragment

;;!! struct Foo {}
(struct_item
  name: (_) @name
) @class @type @name.domain

;;!! impl Foo {}
(impl_item
  !trait
  type: (_) @name
) @name.domain

;;!! impl Foo for Bar {}
(impl_item
  trait: (_) @name
  type: (_) @type
) @name.domain

;;!! impl Foo {}
(impl_item
  body: (_
    "{" @namedFunction.iteration.start.endOf
    "}" @namedFunction.iteration.end.startOf
  )
) @class @type

;;!! enum Foo {}
(enum_item
  name: (_) @name
) @class @type @name.domain

;;!! enum Foo { Bar, Baz }
;;!             ^^^  ^^^
(enum_variant
  name: (_) @name
) @name.domain

;;!! trait Foo {}
(trait_item
  name: (_) @name
) @class @type @name.domain

;;!! mod foo {}
(mod_item
  name: (_) @name
) @name.domain

;;!! fn foo() {}
;;!  ^^^^^^^^^^^
(function_item
  name: (_) @name
) @namedFunction @name.domain

;;!! fn foo() -> int {}
;;!              ^^^
(function_item
  parameters: (_) @_.leading.endOf
  return_type: (_)? @type
) @type.domain

;;!! fn foo<T: Display>() {}
;;!         ^
;;!            ^^^^^^^
(constrained_type_parameter
  left: (_) @name @collectionKey
  (trait_bounds
    ":" @value.leading.endOf
    (_) @value.start
    (_) @value.end
  )
) @_.domain

;;!! where T: Display
;;!        ^
;;!           ^^^^^^^
(where_predicate
  left: (_) @name @collectionKey
  (trait_bounds
    ":" @value.leading.endOf
    (_) @value.start
    (_) @value.end
  )
) @_.domain

;;!! struct Foo { bar: u8 }
(field_declaration
  name: (_) @name @type.leading.endOf
  type: (_) @type
) @statement @_.domain

;;!! (t: &T, u: &U)
;;!   ^      ^
(parameter
  pattern: (_) @name @type.leading.endOf
  type: (_) @type
) @_.domain

[
  (call_expression)
  (macro_invocation)
  (struct_expression)
] @functionCall

(call_expression
  function: (_) @functionCallee
) @_.domain

(closure_expression) @anonymousFunction

;;!! let foo = [1, 2, 3];
;;!! let foo = (1, "2", true);
[
  (array_expression)
  (tuple_expression)
] @list

;;!! match value {}
(match_expression
  value: (_) @value
  body: (_
    "{" @branch.iteration.start.endOf @condition.iteration.start.endOf
    "}" @branch.iteration.end.startOf @condition.iteration.end.startOf
  )
) @value.domain @branch.iteration.domain @condition.iteration.domain

;;!! #[derive(Debug)]
;;!  ^^^^^^^^^^^^^^^^
(attribute_item) @attribute

;;!! &mut x;
;;!   ^^^
(_
  (mutable_specifier) @attribute
  .
  (_) @_.trailing.startOf
) @_.domain

;;!! let Foo {aaa: 1, bbb: 2}
;;!           ^^^     ^^^
;;!                ^       ^
(field_initializer
  name: (_) @collectionKey @value.leading.endOf
  value: (_) @value @collectionKey.trailing.startOf
) @_.domain

;;!! Foo {aaa: 1, bbb: 2}
;;!       ^^^     ^^^
;;!            ^       ^
(field_pattern
  name: (_) @collectionKey @value.leading.endOf
  pattern: (_) @value @collectionKey.trailing.startOf
) @_.domain

;;!! const foo: u8 = 2;
;;!                  ^
(const_item
  name: (_) @name @type.leading.endOf
  type: (_) @type @value.leading.endOf
  value: (_) @value
) @_.domain

;;!! let foo = 2;
;;!      ^^^
;;!            ^
(let_declaration
  pattern: (_) @name @value.leading.start.endOf
  .
  value: (_) @value
) @_.domain

;;!! let foo: u8 = 2;
;;!      ^^^
;;!            ^
(let_declaration
  pattern: (_) @name @type.leading.endOf
  type: (_) @type @value.leading.start.endOf
  value: (_) @value
) @_.domain

;;!! let foo: u8;
;;!      ^^^
;;!            ^
(let_declaration
  pattern: (_) @name @type.leading.endOf
  type: (_) @type
  !value
) @_.domain

(expression_statement
  (assignment_expression
    left: (_) @name @value.leading.start.endOf
    right: (_) @value @name.trailing.start.startOf
  )
) @_.domain

;;!! #[cfg_attr(feature = "foo")]
;;!             ^^^^^^^
;;!                       ^^^^^
(meta_item
  (identifier) @name @value.leading.endOf
  value: (_) @value
) @_.domain

;;!! return 2;
;;!         ^
(expression_statement
  (return_expression
    (_) @value
  )
) @_.domain

;; Implicit return value at end of function body
(function_item
  body: (_
    (_) @value
    .
  )
  (#not-type?
    @value
    ;; Exclude return expression
    return_expression
    ;; Exclude all statements
    associated_type
    attribute_item
    const_item
    empty_statement
    enum_item
    extern_crate_declaration
    foreign_mod_item
    impl_item
    inner_attribute_item
    let_declaration
    macro_definition
    macro_invocation
    function_item
    function_signature_item
    mod_item
    static_item
    struct_item
    trait_item
    type_item
    union_item
    use_declaration
    expression_statement
  )
)

;;!! |x| x + 1
;;!     ^^^^^^
(closure_expression
  body: (_) @value
  (#not-type? @value block)
)

;;!! while v < 0 {}
;;!        ^^^^^
(while_expression
  condition: (_) @condition
) @_.domain

;;!! while let Some(i) = number {}
;;!        ^^^^^^^^^^^^^^^^^^^^
(while_let_expression
  "let" @condition.start
  value: (_) @condition.end
) @_.domain

;;!! match value { 5 => {} }
;;!                ^^^^^^^
(match_arm) @branch

;;!! User { value } if value.use() => {}
;;!                    ^^^^^^^^^^^
(match_arm
  (match_pattern
    (_) @_.leading.endOf
    .
    condition: (_) @condition
  )
) @_.domain

;;!! match value { 5 => {} }
;;!                ^^^^^^^
(match_arm
  (match_pattern
    !condition
  ) @condition
) @condition.domain

;;!! match value { 5 => 0, }
;;!                    ^^^
(match_arm
  "=>" @interior.start.endOf
  value: (_) @_dummy
  (#not-type? @_dummy block)
) @interior.end.endOf

(array_type
  element: (_) @type
) @_.domain
;;!! fn foo(a: u32, b: u32) -> {}
;;!         ^^^^^^  ^^^^^^
(_
  parameters: (_
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! fn foo(a: u32, b: u32) -> {}
;;!         ^^^^^^^^^^^^^^
(_
  parameters: (_
    [
      "("
      "|"
    ] @argumentList.removal.start.endOf @argumentOrParameter.iteration.start.endOf
    [
      ")"
      "|"
    ] @argumentList.removal.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @argumentList
  (#empty-single-multi-delimiter! @argumentList @argumentList "" ", " ",\n")
  (#child-range! @argumentList 1 -2)
) @argumentList.domain @argumentOrParameter.iteration.domain

(_
  parameters: (_
    [
      "("
      "|"
    ] @name.iteration.start.endOf @type.iteration.start.endOf
    [
      ")"
      "|"
    ] @name.iteration.end.startOf @type.iteration.end.startOf
  )
)

;;!! foo(aaa, bbb)
;;!      ^^^  ^^^
(_
  (arguments
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! foo(aaa, bbb)
;;!      ^^^^^^^^
(_
  (arguments
    "(" @argumentList.removal.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.removal.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @argumentList
  (#empty-single-multi-delimiter! @argumentList @argumentList "" ", " ",\n")
  (#child-range! @argumentList 1 -2)
) @argumentList.domain @argumentOrParameter.iteration.domain

;;!! enum E { C(u16, u16) }
;;!             ^^^  ^^^
(_
  (ordered_field_declaration_list
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! enum E { C(u16, u16) }
;;!             ^^^^^^^^
(_
  (ordered_field_declaration_list
    "(" @argumentList.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @_dummy
  (#empty-single-multi-delimiter! @argumentList.start.endOf @_dummy "" ", " ",\n")
) @argumentList.domain @argumentOrParameter.iteration.domain

;;!! #[derive(aaa, bbb)]
;;!           ^^^  ^^^
(_
  (meta_arguments
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! #[derive(aaa, bbb)]
;;!           ^^^^^^^^
(_
  (meta_arguments
    "(" @argumentList.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @_dummy
  (#empty-single-multi-delimiter! @argumentList.start.endOf @_dummy "" ", " ",\n")
) @argumentList.domain @argumentOrParameter.iteration.domain

;;!! fn foo<T: Display, U: Clone>() {}
;;!         ^^^^^^^^^^  ^^^^^^^^
(_
  (type_parameters
    (_)? @_.leading.endOf
    .
    (_) @type
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @type @_dummy ", " ",\n")
)

;;!! fn foo<T: Display, U: Clone>() {}
;;!         ^^^^^^^^^^^^^^^^^^^^
(_
  (type_parameters
    "<" @type.iteration.start.endOf
    ">" @type.iteration.end.startOf
  )
) @type.iteration.domain

;;!! where T: Display, U: Clone
;;!        ^^^^^^^^^^  ^^^^^^^^
(
  (where_clause
    (_)? @_.leading.endOf
    .
    (_) @type
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @type @_dummy ", " ",\n")
)

;;!! where T: Display, U: Clone
;;!        ^^^^^^^^^^^^^^^^^^^^
(where_clause
  "where" @type.iteration.start.endOf
) @type.iteration.end.endOf @type.iteration.domain

;;!! for v in values {}
(for_expression
  pattern: (_) @name
  value: (_) @value
) @_.domain

;;!! type Foo = Bar;
(type_item
  name: (_) @value.leading.endOf
  type: (_) @value
) @type @value.domain

;;!! let foo: Foo<i32, u64>;
;;!               ^^^  ^^^
(generic_type
  (type_arguments
    (_)? @_.leading.endOf
    .
    (_) @type
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @type @_dummy ", " ",\n")
)

;;!! <i32, u64>;
(type_arguments
  "<" @type.iteration.start.endOf
  ">" @type.iteration.end.startOf
)

;;!! foo as i32
(type_cast_expression
  value: (_) @type.leading.endOf
  type: (_) @type
) @_.domain

operator: [
  "<"
  "<<"
  "<<="
  "<="
  ">"
  ">="
  ">>"
  ">>="
] @disqualifyDelimiter
(function_item
  "->" @disqualifyDelimiter
)
(match_arm
  "=>" @disqualifyDelimiter
)
(macro_rule
  "=>" @disqualifyDelimiter
)
(lifetime
  "'" @disqualifyDelimiter
)
