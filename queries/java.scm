;; https://github.com/tree-sitter/tree-sitter-java/blob/master/src/grammar.json

;; Generated by the following command:
;; > curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-java/master/src/node-types.json | jq '[.[] | select(.type == "statement" or .type == "declaration") | .subtypes[].type]'
[
  (annotation_type_declaration)
  (class_declaration)
  (import_declaration)
  (interface_declaration)
  (module_declaration)
  (package_declaration)
  (assert_statement)
  (break_statement)
  (continue_statement)
  (do_statement)
  (enhanced_for_statement)
  (expression_statement)
  (for_statement)
  (labeled_statement)
  (return_statement)
  (switch_expression)
  (synchronized_statement)
  (throw_statement)
  (try_statement)
  (try_with_resources_statement)
  (while_statement)
  (yield_statement)
  (method_declaration)
  (constructor_declaration)
  (field_declaration)
  (constant_declaration)
  (static_initializer)
  (record_declaration)

  ;; Disabled on purpose. We don't consider these to be statements.
  ;; exceptions
  ;; ";",
  ;; "block",
  ;; (declaration) ;; Is a compound of all other declarations

  ;; Disabled on purpose. We have a better definition of this below.
  ;; (if_statement)
  ;; (enum_declaration)
] @statement

(
  (local_variable_declaration) @statement
  (#not-parent-type? @statement for_statement)
)

;;!! enum Foo {}
;;!  ^^^^^^^^^^^
;;!       ^^^
(enum_declaration
  name: (_) @name
  body: (_
    "{" @name.iteration.start.endOf
    "}" @name.iteration.end.startOf
  )
) @statement @type @name.domain

;;!! { }
;;!   ^
(_
  .
  "{" @interior.start.endOf
  "}" @interior.end.startOf
  .
)

;;!! enum Foo { bar, baz }
;;!             ^^^  ^^^
(enum_constant
  name: (_) @name
) @_.domain

;;!! class Foo {}
;;!  ^^^^^^^^^^^^
;;!        ^^^
(class_declaration
  name: (_) @name
) @class @type @_.domain

;;!! interface Foo {}
;;!  ^^^^^^^^^^^^^^^^
;;!            ^^^
(interface_declaration
  name: (_) @name
) @type @_.domain

(
  (program) @class.iteration @statement.iteration
  (#document-range! @class.iteration @statement.iteration)
)

(
  (program) @name.iteration @value.iteration @type.iteration
  (#document-range! @name.iteration @value.iteration @type.iteration)
)

;;!! class MyClass { }
;;!                 ^
(class_body
  "{" @class.iteration.start.endOf @namedFunction.iteration.start.endOf
  "}" @class.iteration.end.startOf @namedFunction.iteration.end.startOf
)

;;!! { }
;;!   ^
(
  (_
    "{" @name.iteration.start.endOf @value.iteration.start.endOf @type.iteration.start.endOf
    "}" @name.iteration.end.startOf @value.iteration.end.startOf @type.iteration.end.startOf
  ) @_dummy
  (#type? @_dummy block class_body interface_body constructor_body)
)

(
  (_
    "{" @statement.iteration.start.endOf
    "}" @statement.iteration.end.startOf
  ) @_dummy
  (#type? @_dummy block class_body interface_body constructor_body)
)

;;!! void myFunk() {}
;;!  ^^^^^^^^^^^^^^^^
(method_declaration
  name: (_) @name
) @namedFunction @name.domain

(constructor_declaration
  name: (_) @name
) @namedFunction @name.domain

;;!! "string"
;;!  ^^^^^^^^
;;!! """string"""
;;!  ^^^^^^^^^^^^
(string_literal) @string

[
  (string_fragment)
  (multiline_string_fragment)
] @textFragment

;;!! // comment
;;!  ^^^^^^^^^^
[
  (line_comment)
  (block_comment)
] @comment @textFragment

;;!! int[] values = {1, 2, 3};
;;!                 ^^^^^^^^^
(array_initializer) @list

;;!! List<String> value = new ArrayList() {{ add("a"); }};
;;!                                       ^^^^^^^^^^^^^^^
(object_creation_expression
  (class_body
    (block) @map
  )
)

;;!! foo(1);
;;!  ^^^^^^
;;!! new Foo(1);
;;!  ^^^^^^^^^^
;;!! super(1);
;;!  ^^^^^^^^
[
  (method_invocation)
  (object_creation_expression)
  (explicit_constructor_invocation)
] @functionCall

;;!! case "0": return "zero";
;;!  ^^^^^^^^^^^^^^^^^^^^^^^^
;;!! case "0" -> "zero";
;;!  ^^^^^^^^^^^^^^^^^^^
[
  (switch_block_statement_group)
  (switch_rule)
] @branch

;;!! case "0": return "zero";
;;!       ^^^
;;!  ------------------------
(switch_block_statement_group
  (switch_label
    (_) @condition
  )
  (#allow-multiple! @condition)
) @condition.domain

;;!! case "0" -> "zero";
;;!       ^^^
;;!  -------------------
(switch_rule
  (switch_label
    (_) @condition
  )
) @condition.domain

;;!! case "0" -> "zero";
;;!             ^^^^^^^^
(switch_rule
  "->" @interior.start.endOf
  (expression_statement)
) @interior.end.endOf

;;!! case 0: break;
;;!         ^^^^^^^
(switch_block_statement_group
  ":" @interior.start.endOf
  (_) @_dummy
  (#not-type? @_dummy block)
) @interior.end.endOf

(switch_expression
  body: (_
    "{" @branch.iteration.start.endOf @condition.iteration.start.endOf
    "}" @condition.iteration.end.startOf @branch.iteration.end.startOf
  )
) @condition.iteration.domain @branch.iteration.domain

;;!! if () {} else {}
;;!  ^^^^^^^^^^^^^^^^
(
  (if_statement) @ifStatement @statement @branch.iteration
  (#not-parent-type? @ifStatement if_statement)
)

;;!! if () {}
;;!  ^^^^^^^^
(
  (if_statement
    "if" @branch.start @branch.removal.start
    condition: (_) @condition
    consequence: (_) @branch.end @branch.removal.end
    "else"? @branch.removal.end.startOf
    alternative: (if_statement)? @branch.removal.end.startOf
  ) @condition.domain
  (#not-parent-type? @condition.domain if_statement)
  (#child-range! @condition 0 -1 true true)
)

;;!! else if () {}
;;!  ^^^^^^^^^^^^^
(if_statement
  "else" @branch.start @condition.domain.start
  alternative: (if_statement
    condition: (_) @condition
    consequence: (block) @branch.end @condition.domain.end
    (#child-range! @condition 0 -1 true true)
  )
)

;;!! else {}
;;!  ^^^^^^^
(if_statement
  "else" @branch.start
  alternative: (block) @branch.end
)

;;!! try {}
;;!  ^^^^^^
(try_statement
  "try" @branch.start
  body: (_) @branch.end
)

;;!! catch (Exception e) {}
;;!  ^^^^^^^^^^^^^^^^^^^^^^
(catch_clause) @branch

;;!! finally {}
;;!  ^^^^^^^^^^
(finally_clause) @branch

(try_statement) @branch.iteration

;;!! for (int i = 0; i < 5; ++i) {}
;;!                  ^^^^^
;;!  ------------------------------
(for_statement
  condition: (_) @condition
) @branch @_.domain

;;!! while (value) {}
;;!         ^^^^^
;;!  ----------------
(while_statement
  condition: (_) @condition
  (#child-range! @condition 0 -1 true true)
) @branch @_.domain

(do_statement
  condition: (_) @condition
  (#child-range! @condition 0 -1 true true)
) @branch @_.domain

;;!! switch (value) {}
;;!          ^^^^^
;;!  -----------------
(switch_expression
  condition: (_) @value
  (#child-range! @value 0 -1 true true)
) @_.domain

;;!! true ? 0 : 1
;;!  ^^^^
;;!         ^   ^
(ternary_expression
  condition: (_) @condition
  consequence: (_) @branch
) @condition.domain @branch.iteration
(ternary_expression
  alternative: (_) @branch
)

;;!! void myFunk(int value) {}
;;!                  ^^^^^
;;!  -------------------------
(formal_parameter
  (identifier) @name
) @_.domain

;;!! void myFunk(int value) {}
;;!              ^^^^^^^^^
(formal_parameters
  "(" @type.iteration.start.endOf @name.iteration.start.endOf
  ")" @type.iteration.end.startOf @name.iteration.end.startOf
)

;;!! List<String> list = value;
;;!  ^^^^^^^^^^^^
;;!  --------------------------
(local_variable_declaration
  type: (_) @type
) @_.domain

;;!! name = new ArrayList<String>();
;;!             ^^^^^^^^^^^^^^^^^
;;!         -----------------------
(object_creation_expression
  type: (_) @type
) @_.domain

;;!! name = new int[5];
;;!             ^^^
;;!         ----------
(array_creation_expression
  type: (_) @type
) @_.domain

;;!! void myFunk(int value) {}
;;!              ^^^
;;!              ---------
(formal_parameter
  type: (_) @type
) @_.domain

;;!! int size() {}
;;!  ^^^
;;!  -------------
(method_declaration
  type: (_) @type
) @_.domain

;;!! (int)5
;;!   ^^^
(cast_expression
  "(" @type.removal.start
  type: (_) @type
  ")" @type.removal.end
) @_.domain

;;!! new test();
;;!  ^^^^^^^^
;;!  ----------
(_
  (object_creation_expression
    (argument_list) @functionCallee.end.startOf
  ) @functionCallee.start.startOf @_.domain
)

;;!! new test().bar();
;;!  ^^^^^^^^^^^^^^
;;!  ----------------
(_
  (method_invocation
    (argument_list) @functionCallee.end.startOf
  ) @functionCallee.start.startOf @_.domain
)

;;!! super();
;;!  ^^^^^
;;!  --------
(explicit_constructor_invocation
  (argument_list) @functionCallee.end.startOf
) @functionCallee.start.startOf @_.domain

;;!! for (int value : values) {}
;;!                   ^^^^^^
;;!  ---------------------------
(enhanced_for_statement
  type: (_) @type
  name: (_) @name
  value: (_) @value
) @branch @_.domain

;;!! int value = 0;
;;!              ^
;;!           xxxx
;;!  --------------
(local_variable_declaration
  (variable_declarator
    name: (_) @name @value.leading.endOf
    value: (_)? @value @name.trailing.startOf
  )
) @_.domain

;;!! int value = 0;
;;!  ^^^
;;!      ^^^^^
;;!              ^
(field_declaration
  type: (_) @type
  (variable_declarator
    name: (_) @name @value.leading.endOf
    value: (_)? @value @name.trailing.startOf
  )
) @_.domain

;;!! int value;
;;!  ^^^
;;!      ^^^^^
(constant_declaration
  type: (_) @type
  (variable_declarator
    name: (_) @name
  )
) @_.domain

;;!! int foo, bar;
;;!      ^^^  ^^^
(
  (local_variable_declaration
    type: (_)
    (variable_declarator)? @_.leading.endOf
    .
    (variable_declarator) @collectionItem
    .
    (variable_declarator)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @collectionItem @_dummy ", " ",\n")
)

(
  (field_declaration
    type: (_)
    (variable_declarator)? @_.leading.endOf
    .
    (variable_declarator) @collectionItem
    .
    (variable_declarator)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @collectionItem @_dummy ", " ",\n")
)

;;!! int foo, bar;
;;!      ^^^^^^^^
;;!  -------------
(local_variable_declaration
  type: (_)
  .
  (_) @collectionItem.iteration.start.startOf
  ";"? @collectionItem.iteration.end.startOf
) @collectionItem.iteration.domain

(field_declaration
  type: (_)
  .
  (_) @collectionItem.iteration.start.startOf
  ";"? @collectionItem.iteration.end.startOf
) @collectionItem.iteration.domain

;;!! throws Exception, IOException
;;!         ^^^^^^^^^  ^^^^^^^^^^^
(
  (throws
    (_)? @_.leading.endOf
    .
    (_) @collectionItem
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @collectionItem @_dummy ", " ",\n")
)

(
  (throws
    .
    (_) @collectionItem.iteration.start.startOf
  ) @collectionItem.iteration.end.endOf
)
 @collectionItem.iteration.domain

;;!! value = 1;
;;!          ^
;;!       xxxx
;;!  ----------
(_
  (assignment_expression
    left: (_) @name @value.leading.endOf
    right: (_) @value @name.trailing.startOf
  ) @_.domain.start
  ";"? @_.domain.end
)

;;!! return value;
;;!         ^^^^^
;;!  -------------
(
  (return_statement
    (_) @value
  ) @_.domain
)

;;!! yield value;
;;!        ^^^^^
;;!  ------------
(yield_statement
  (_) @value
) @_.domain

;;!! ((value) -> true)
;;!   ^^^^^^^^^^^^^^^
(lambda_expression) @anonymousFunction

;;!! str -> str.length > 0
;;!         ^^^^^^^^^^^^^^
;;!  ---------------------
(lambda_expression
  body: (_) @value
  (#not-type? @value block)
) @_.domain

;;!! Map<int, int> foo;
;;!      ^^^  ^^^
(
  (type_arguments
    (_)? @_.leading.endOf
    .
    (_) @type
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @type @_dummy ", " ",\n")
)

;;!! Map<int, int> foo;
;;!      ^^^^^^^^
(type_arguments
  "<" @type.iteration.start.endOf
  ">" @type.iteration.end.startOf
)

;;!! void foo(int aaa, int bbb) {}
;;!           ^^^^^^^  ^^^^^^^
(_
  parameters: (_
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#not-type? @argumentOrParameter "block_comment")
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! foo(aaa, bbb);
;;!      ^^^  ^^^
(
  (argument_list
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#not-type? @argumentOrParameter "block_comment")
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! void foo(int aaa, int bbb) {}
;;!           ^^^^^^^^^^^^^^^^
(_
  parameters: (_
    "(" @argumentList.removal.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.removal.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @argumentList
  (#empty-single-multi-delimiter! @argumentList @argumentList "" ", " ",\n")
  (#child-range! @argumentList 1 -2)
) @argumentList.domain @argumentOrParameter.iteration.domain

;;!! foo(aaa, bbb);
;;!      ^^^^^^^^
(_
  (argument_list
    "(" @argumentList.removal.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.removal.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @argumentList
  (#empty-single-multi-delimiter! @argumentList @argumentList "" ", " ",\n")
  (#child-range! @argumentList 1 -2)
) @argumentList.domain @argumentOrParameter.iteration.domain

;;!! catch(Exception ex) {}
;;!        ^^^^^^^^^^^^
;;!        ^^^^^^^^^
;;!                  ^^
(catch_formal_parameter
  (catch_type) @type
  name: (_) @name
) @argumentOrParameter @_.domain

;;!! try (PrintWriter writer = create()) { }
;;!       ^^^^^^^^^^^ ^^^^^    ^^^^^^^^
(try_with_resources_statement
  (resource_specification
    (resource
      type: (_) @type
      name: (_) @name
      value: (_) @value
    )
  )
) @_.domain

operator: [
  "<"
  "<<"
  "<<="
  "<="
  ">"
  ">="
  ">>"
  ">>="
  ">>>"
  ">>>="
] @disqualifyDelimiter
(lambda_expression
  "->" @disqualifyDelimiter
)
(switch_rule
  "->" @disqualifyDelimiter
)
