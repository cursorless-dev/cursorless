;; Generated by the following command:
;;  >  curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-cpp/master/src/node-types.json | jq '[.[] | select(.type == "compound_statement") | .children.types[].type] + [.[] | select(.type == "_statement") | .subtypes[].type]'
[
  (declaration)
  (function_definition)
  (linkage_specification)
  (preproc_call)
  (preproc_def)
  (preproc_function_def)
  (preproc_if)
  (preproc_ifdef)
  (preproc_include)
  (type_definition)
  (break_statement)
  (case_statement)
  (compound_statement)
  (continue_statement)
  (do_statement)
  (expression_statement)
  (for_statement)
  (goto_statement)
  (if_statement)
  (labeled_statement)
  (return_statement)
  (switch_statement)
  (while_statement)
] @statement

(if_statement) @ifStatement

(
  (string_literal) @string @textFragment
  (#child-range! @textFragment 0 -1 true true)
)

(comment) @comment @textFragment

[
  (struct_specifier)
  (enum_specifier)
  (union_specifier)
] @class

(_
  (struct_specifier
    name: (_) @className
  ) @_.domain.start
  ";"? @_.domain.end
)
(_
  (enum_specifier
    name: (_) @className
  ) @_.domain.start
  ";"? @_.domain.end
)
(_
  (union_specifier
    name: (_) @className
  ) @_.domain.start
  ";"? @_.domain.end
)

;;!! void funcName();
(declaration
  (function_declarator
    declarator: (_
      !name
    ) @functionName @name
  )
) @namedFunction @functionName.domain @name.domain

;;!! void C::funcName() {}
(declaration
  (function_declarator
    declarator: (_
      name: (_) @functionName @name
    )
  )
) @namedFunction @functionName.domain @name.domain

(function_definition
  declarator: (_
    declarator: (_
      name: (_) @functionName @name
    )
  )
) @namedFunction @functionName.domain @name.domain

;;!! void funcName() {}
(function_definition
  declarator: (_
    declarator: (_
      !name
    ) @functionName @name
  )
) @namedFunction @name.domain

(initializer_list) @list

(call_expression) @functionCall
(declaration
  (init_declarator) @functionCall
) @_.domain

(call_expression
  function: (_) @functionCallee
) @_.domain
(declaration
  (init_declarator
    declarator: (_) @functionCallee
  )
) @_.domain

(switch_statement
  condition: (_
    value: (_) @private.switchStatementSubject
  )
) @_.domain

;;!! int aaa = 0;
(declaration
  declarator: (_
    declarator: (_) @name @value.leading.endOf
    value: (_) @value
  )
) @_.domain

;;!! int aaa;
(declaration
  declarator: (_
    !declarator
  ) @name
) @_.domain

;;!! aaa = 0;
(
  (assignment_expression
    left: (_) @name @value.leading.endOf
    right: (_) @value @name.trailing.startOf
  ) @_.domain.start
  ";"? @_.domain.end
)

;; >  curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-cpp/master/src/node-types.json | jq '[.[] | select(.type == "_type_specifier") | .subtypes[].type]'
[
  (enum_specifier)
  (primitive_type)
  (sized_type_specifier)
  (struct_specifier)
  (type_identifier)
  (union_specifier)
] @type

(_
  type: (_) @type
) @_.domain

;;!! void foo(int value) {}
;;!               ^^^^^
(parameter_declaration
  declarator: (_) @name
) @_.domain

;;!! void foo(int value) {}
;;!           ^^^^^^^^^
(parameter_list
  (_)? @_.leading.endOf
  .
  (parameter_declaration) @argumentOrParameter
  .
  (_)? @_.trailing.startOf
)

;;!! foo(5 + 6)
;;!      ^^^^^
(argument_list
  (_)? @_.leading.endOf
  .
  (_) @argumentOrParameter
  .
  (_)? @_.trailing.startOf
)

(_
  (function_declarator
    (parameter_list
      "(" @argumentOrParameter.iteration.start.endOf @name.iteration.start.endOf @value.iteration.start.endOf
      ")" @argumentOrParameter.iteration.end.startOf @name.iteration.end.startOf @value.iteration.end.startOf
    )
  )
) @argumentOrParameter.iteration.domain

(argument_list
  "(" @argumentOrParameter.iteration.start.endOf @name.iteration.start.endOf @value.iteration.start.endOf
  ")" @argumentOrParameter.iteration.end.startOf @name.iteration.end.startOf @value.iteration.end.startOf
) @argumentOrParameter.iteration.domain
