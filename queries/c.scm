;; https://github.com/tree-sitter/tree-sitter-c/blob/master/src/grammar.json

;; Generated by the following command:
;;  >  curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-cpp/master/src/node-types.json | jq '[.[] | select(.type == "compound_statement") | .children.types[].type] + [.[] | select(.type == "_statement") | .subtypes[].type]'
[
  (declaration)
  (function_definition)
  (linkage_specification)
  (preproc_call)
  (preproc_def)
  (preproc_function_def)
  (preproc_if)
  (preproc_ifdef)
  (preproc_include)
  (type_definition)
  (break_statement)
  (case_statement)
  (continue_statement)
  (do_statement)
  (expression_statement)
  (for_statement)
  (goto_statement)
  (if_statement)
  (labeled_statement)
  (return_statement)
  (switch_statement)
  (while_statement)
  ;; Disabled on purpose. This is commonly the entire body of a function.
  ;; (compound_statement)
] @statement

(if_statement) @ifStatement

(
  (translation_unit) @statement.iteration @class.iteration @className.iteration
  (#document-range! @statement.iteration @class.iteration @className.iteration)
)
(
  (translation_unit) @namedFunction.iteration @functionName.iteration
  (#document-range! @namedFunction.iteration @functionName.iteration)
)

(_
  body: (_
    "{" @statement.iteration.start.endOf
    "}" @statement.iteration.end.startOf
  )
)

(
  (string_literal) @string @textFragment
  (#child-range! @textFragment 0 -1 true true)
)

(comment) @comment @textFragment

(_
  (struct_specifier
    name: (_) @className @name
    body: (_)
  ) @_.domain.start @class.start @type.start
  .
  ";"? @_.domain.end @class.end @type.end
)
(_
  (enum_specifier
    name: (_) @className @name
    body: (_)
  ) @_.domain.start @class.start @type.start
  .
  ";"? @_.domain.end @class.end @type.end
)
(_
  (union_specifier
    name: (_) @className @name
    body: (_)
  ) @_.domain.start @class.start @type.start
  .
  ";"? @_.domain.end @class.end @type.end
)

(_
  (struct_specifier
    name: (_)
    body: (_)
  ) @statement.start
  .
  ";"? @statement.end
)
(_
  (enum_specifier
    name: (_)
    body: (_)
  ) @statement.start
  .
  ";"? @statement.end
)
(_
  (union_specifier
    name: (_)
    body: (_)
  ) @statement.start
  .
  ";"? @statement.end
)

(type_definition
  type: (struct_specifier
    body: (_)
  )
  declarator: (type_identifier) @className @name
) @_.domain @class @type
(type_definition
  type: (enum_specifier
    body: (_)
  )
  declarator: (type_identifier) @className @name
) @_.domain @class @type
(type_definition
  type: (union_specifier
    body: (_)
  )
  declarator: (type_identifier) @className @name
) @_.domain @class @type

;;!! void funcName();
(declaration
  (function_declarator
    declarator: (_
      !name
    ) @functionName @name
  )
) @namedFunction @functionName.domain @name.domain

;;!! void C::funcName() {}
(declaration
  (function_declarator
    declarator: (_
      name: (_) @functionName @name
    )
  )
) @namedFunction @functionName.domain @name.domain

(function_definition
  declarator: (_
    declarator: (_
      name: (_) @functionName @name
    )
  )
) @namedFunction @functionName.domain @name.domain

;;!! void funcName() {}
(function_definition
  declarator: (_
    declarator: (_
      !name
    ) @functionName @name
  )
) @namedFunction @functionName.domain @name.domain

(field_declaration
  declarator: (_
    !declarator
  ) @name
) @name.domain
(field_declaration
  declarator: (_
    declarator: (_) @name
  )
) @name.domain

(initializer_list) @list

(call_expression) @functionCall

(call_expression
  function: (_) @functionCallee
) @_.domain

(switch_statement
  condition: (_
    "(" @private.switchStatementSubject.start.endOf
    ")" @private.switchStatementSubject.end.startOf
  )
) @_.domain

;;!! int aaa = 0;
(declaration
  declarator: (_
    declarator: (_) @name @value.leading.endOf
    value: (_) @value @name.removal.end.startOf
  )
) @_.domain @name.removal.start.startOf

;;!! int aaa;
(declaration
  declarator: (_
    !declarator
  ) @name
) @_.domain

;;!! aaa = 0;
(_
  (assignment_expression
    left: (_) @name @value.leading.endOf
    right: (_) @value @name.trailing.startOf
  ) @_.domain.start
  .
  ";"? @_.domain.end
)

(
  (_
    type: (_) @type
  ) @_.domain
  (#not-type? @_.domain type_definition type_descriptor cast_expression)
)

;;!! (int)5.5;
;;!   ^^^
(cast_expression
  "(" @type.removal.start
  type: (_) @type
  ")" @type.removal.end
) @_.domain

;;!! void foo(int aaa) {}
;;!               ^^^
(parameter_declaration
  declarator: (_) @name
) @_.domain

;;!! void foo(int aaa, int bbb) {}
;;!           ^^^^^^^  ^^^^^^^
(
  (parameter_list
    (_)? @_.leading.endOf
    .
    (parameter_declaration) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#not-type? @argumentOrParameter "comment")
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! foo(aaa, bbb);
;;!      ^^^  ^^^
(
  (argument_list
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#not-type? @argumentOrParameter "comment")
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;;!! void foo(int aaa, int bbb) {}
;;!           ^^^^^^^^^^^^^^^^
(_
  (function_declarator
    (parameter_list
      "(" @argumentList.start.endOf @argumentOrParameter.iteration.start.endOf
      ")" @argumentList.end.startOf @argumentOrParameter.iteration.end.startOf
    ) @_dummy
  )
  (#empty-single-multi-delimiter! @argumentList.start.endOf @_dummy "" ", " ",\n")
) @argumentList.domain @argumentOrParameter.iteration.domain

(parameter_list
  "(" @type.iteration.start.endOf @name.iteration.start.endOf @value.iteration.start.endOf
  ")" @type.iteration.end.startOf @name.iteration.end.startOf @value.iteration.end.startOf
)

;;!! foo(aaa, bbb);
;;!      ^^^^^^^^
(_
  (argument_list
    "(" @argumentList.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @_dummy
  (#empty-single-multi-delimiter! @argumentList.start.endOf @_dummy "" ", " ",\n")
) @argumentList.domain @argumentOrParameter.iteration.domain

operator: [
  "->"
  "<"
  "<<"
  "<<="
  "<="
  ">"
  ">="
  ">>"
  ">>="
] @disqualifyDelimiter
