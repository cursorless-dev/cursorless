;; https://github.com/tree-sitter/tree-sitter-c-sharp/blob/master/src/grammar.json

;; Generated by the following command:
;; > curl https://raw.githubusercontent.com/tree-sitter/tree-sitter-c-sharp/master/src/node-types.json \
;;   | jq '.[] | select(.type == "_statement" or .type == "_declaration") | [.subtypes[].type]'
[
  (class_declaration)
  (constructor_declaration)
  (conversion_operator_declaration)
  (delegate_declaration)
  (destructor_declaration)
  (enum_declaration)
  (event_declaration)
  (event_field_declaration)
  (field_declaration)
  (indexer_declaration)
  (interface_declaration)
  (method_declaration)
  (namespace_declaration)
  (operator_declaration)
  (property_declaration)
  (record_declaration)
  (struct_declaration)
  (using_directive)
  (break_statement)
  (checked_statement)
  (continue_statement)
  (do_statement)
  (empty_statement)
  (expression_statement)
  (fixed_statement)
  (for_each_statement)
  (for_statement)
  (goto_statement)
  (labeled_statement)
  (local_declaration_statement)
  (local_function_statement)
  (lock_statement)
  (return_statement)
  (switch_statement)
  (throw_statement)
  (try_statement)
  (unsafe_statement)
  (using_statement)
  (while_statement)
  (yield_statement)
  ;; Disabled on purpose. This is the entire body of statements.
  ;; (block)
  ;; Disabled on purpose. We have a better definition of this below.
  ;; (if_statement)
] @statement

(
  (compilation_unit) @statement.iteration @class.iteration @namedFunction.iteration
  (#document-range! @statement.iteration @class.iteration @namedFunction.iteration)
)

(
  (compilation_unit) @name.iteration @value.iteration @type.iteration
  (#document-range! @name.iteration @value.iteration @type.iteration)
)

;;!! { }
;;!   ^
(block
  "{" @name.iteration.start.endOf @value.iteration.start.endOf @type.iteration.start.endOf
  "}" @name.iteration.end.startOf @value.iteration.end.startOf @type.iteration.end.startOf
)

(block
  "{" @statement.iteration.start.endOf
  "}" @statement.iteration.end.startOf
)

;;!! { }
;;!   ^
(_
  .
  "{" @interior.start.endOf
  "}" @interior.end.startOf
  .
)

;;!! if () {} else {}
;;!  ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
(
  (if_statement) @ifStatement @statement @branch.iteration
  (#not-parent-type? @ifStatement if_statement)
)

;;!! if () {}
;;!  ^^^^^^^^
(
  (if_statement
    condition: (_) @condition
    consequence: (_) @branch.end.endOf @branch.removal.end.endOf
    "else"? @branch.removal.end.startOf
    alternative: (if_statement)? @branch.removal.end.startOf
  ) @branch.start.startOf @branch.removal.start.startOf @condition.domain
  (#not-parent-type? @condition.domain if_statement)
)

;;!! else if () {}
;;!  ^^^^^^^^^^^^^
(if_statement
  "else" @branch.start.startOf @condition.domain.start.startOf
  (if_statement
    condition: (_) @condition
    consequence: (_) @branch.end.endOf @condition.domain.end.endOf
  )
)

;;!! else {}
;;!  ^^^^^^^
(if_statement
  "else" @branch.start
  alternative: (block) @branch.end
)

;;!! try () {}
;;!  ^^^^^^^^^
(try_statement
  body: (_) @branch.end.endOf
) @branch.start.startOf

;;!! catch () {}
;;!  ^^^^^^^^^^^
(catch_clause) @branch

;;!! finally {}
;;!  ^^^^^^^^^^
(finally_clause) @branch

;;!! try () {} catch () {} finally {}
;;!  ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
(try_statement) @branch.iteration

[
  (for_statement)
  (for_each_statement)
  (while_statement)
  (do_statement)
] @branch

;;!! foreach (int value in values) {}
;;!               ^^^^^
(for_each_statement
  left: (_) @name
  right: (_) @value
) @_.domain

;;!! true ? 0 : 1;
;;!  ^^^^
;;!         ^   ^
(conditional_expression
  condition: (_) @condition
  consequence: (_) @branch
) @condition.domain @branch.iteration
(conditional_expression
  alternative: (_) @branch
)

;;!! class Foo {}
;;!  ^^^^^^^^^^^^
(class_declaration
  name: (identifier) @name
) @class @type @_.domain

(class_declaration
  body: (_
    "{" @class.iteration.start.endOf @name.iteration.start.endOf @statement.iteration.start.endOf
    "}" @class.iteration.end.startOf @name.iteration.end.startOf @statement.iteration.end.startOf
  )
)

(class_declaration
  body: (_
    "{" @name.iteration.start.endOf @value.iteration.start.endOf @type.iteration.start.endOf
    "}" @name.iteration.end.startOf @value.iteration.end.startOf @type.iteration.end.startOf
  )
)

;;!! interface IFoo {}
;;!  ^^^^^^^^^^^^^^^^^
(interface_declaration) @type

(interface_declaration
  body: (_
    "{" @statement.iteration.start.endOf @name.iteration.start.endOf @type.iteration.start.endOf
    "}" @statement.iteration.end.startOf @name.iteration.end.startOf @type.iteration.end.startOf
  )
)

;;!! "Hello world"
(
  (string_literal) @string @textFragment
  (#child-range! @textFragment 0 -1 true true)
)

;;!! @"Hello world"
(
  (verbatim_string_literal) @string @textFragment
  (#character-range! @textFragment 2 -1)
)

;;!! // Hello world
(comment) @comment @textFragment

;;!! () => {};
;;!  ^^^^^^^^
(lambda_expression) @anonymousFunction

;;!! () => 2;
;;!        ^
(lambda_expression
  body: (_) @value
  (#not-type? @value block initializer_expression)
) @_.domain

;;!! return 2;
;;!         ^
(return_statement
  (_) @value
) @_.domain

;;!! yield return 2;
;;!               ^
(yield_statement
  (_) @value
) @_.domain

;;!! [Obsolete("Deprecated")]
;;!   ^^^^^^^^^^^^^^^^^^^^^^
(attribute) @attribute

[
  (delegate_declaration
    name: (_) @name
  )
  (local_function_statement
    name: (_) @name
  )
  (method_declaration
    name: (_) @name
  )
  (constructor_declaration
    name: (_) @name
  )
] @namedFunction @name.domain

(class_declaration
  body: (_
    "{" @namedFunction.iteration.start.endOf @name.iteration.start.endOf
    "}" @namedFunction.iteration.end.startOf @name.iteration.end.startOf
  )
)

;;!! void foo() {}
[
  (invocation_expression)
  (object_creation_expression)
] @functionCall

[
  (invocation_expression
    function: (_) @functionCallee
  )
  (object_creation_expression
    "new" @functionCallee.start
    type: (_) @functionCallee.end
  )
] @functionCallee.domain

(switch_statement
  (tuple_expression) @value
) @value.domain

(switch_statement
  value: (_) @value
) @value.domain

(_
  condition: (_) @condition
  (#not-parent-type? @condition if_statement)
) @condition.domain

(while_statement
  .
  (_) @condition
) @condition.domain

(do_statement
  "while"
  .
  (_) @condition
) @condition.domain

;;!! case 0: break;
;;!  ^^^^^^^^^^^^^^
(switch_section) @branch

;;!! case 0: break;
;;!       ^
(switch_section
  (case_switch_label
    .
    (_) @condition
  )
) @condition.domain

;;!! default: break;
;;!          ^^^^^^^
(switch_section
  [
    (case_switch_label)
    (default_switch_label)
  ] @interior.start.endOf
  .
  (_) @_dummy
  (#not-type? @_dummy block)
) @interior.end.endOf

(switch_statement
  body: (switch_body
    "{" @branch.iteration.start.endOf @condition.iteration.start.endOf
    "}" @branch.iteration.end.startOf @condition.iteration.end.startOf
  )
) @branch.iteration.domain

(object_creation_expression
  initializer: (_) @map @list
)

(implicit_object_creation_expression
  (initializer_expression) @map
)

(array_creation_expression
  (initializer_expression) @list
)
(implicit_array_creation_expression
  (initializer_expression) @list
)
(implicit_stack_alloc_array_creation_expression
  (initializer_expression) @list
)
(stack_alloc_array_creation_expression
  (initializer_expression) @list
)

;;!! FirstName = "Craig"
;;!  ^^^^^^^^^
(initializer_expression
  (assignment_expression
    left: (_) @collectionKey
  ) @_.domain
)

(initializer_expression
  "{" @collectionKey.iteration.start.endOf @value.iteration.start.endOf
  "}" @collectionKey.iteration.end.startOf @value.iteration.end.startOf
)

;;!! String aaa;
;;!         ^^^
(_
  (variable_declaration
    type: (_) @type
    (variable_declarator
      (identifier) @name
    )
  )
) @_.domain

;;!! String aaa = "bbb";
;;!         ^^^
(_
  (variable_declaration
    type: (_) @type
    (variable_declarator
      (identifier) @name @value.leading.endOf
      (equals_value_clause
        (_) @value
      )
    )
  )
) @_.domain

;;!! aaa = "bbb";
;;!  ^^^
;;!! foo = 2;
;;!        ^
(_
  (assignment_expression
    left: (_) @name @value.leading.endOf
    right: (_) @value
  ) @_.domain.start
  .
  ";"? @_.domain.end
)

(_
  name: (_) @name
  (#not-parent-type? @name catch_declaration)
) @_.domain

(
  (_
    type: (_) @type
  ) @_.domain
  (#not-type? @_.domain catch_declaration cast_expression variable_declaration)
)

;;!! (int)5.5;
;;!   ^^^
(cast_expression
  "(" @type.removal.start
  type: (_) @type
  ")" @type.removal.end
) @_.domain

;;!! enum Foo {}
(enum_declaration
  body: (_
    "{" @name.iteration.start.endOf @value.iteration.start.endOf
    "}" @name.iteration.end.startOf @value.iteration.end.startOf
  )
) @type

;;!! bar = 0
;;!        ^
(enum_member_declaration
  name: (_) @value.leading.endOf
  value: (_) @value
) @_.domain

;; Dictionary<string, int> values;
;;!           ^^^^^^  ^^^
(type_argument_list
  (_)? @_.leading.endOf
  .
  (_) @type
  .
  (_)? @_.trailing.startOf
  (#insertion-delimiter! @type ", ")
)

(type_argument_list
  "<" @type.iteration.start.endOf
  ">" @type.iteration.end.startOf
)

;;!! int value = 0;
;;!              ^
(parameter
  name: (_) @value.leading.endOf
  (equals_value_clause
    (_) @value
  )
) @_.domain

;; !! foo(aaa, bbb)
;; !      ^^^  ^^^
(
  (argument_list
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#not-type? @argumentOrParameter "comment")
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;; !! foo(aaa, bbb)
;; !      ^^^^^^^^
(_
  (argument_list
    "(" @argumentList.removal.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.removal.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @argumentList
  (#empty-single-multi-delimiter! @argumentList @argumentList "" ", " ",\n")
  (#child-range! @argumentList 1 -2)
) @argumentList.domain @argumentOrParameter.iteration.domain

;; !! void foo(int a, int b)
;; !           ^^^^^  ^^^^^
(
  (parameter_list
    (_)? @_.leading.endOf
    .
    (_) @argumentOrParameter
    .
    (_)? @_.trailing.startOf
  ) @_dummy
  (#not-type? @argumentOrParameter "comment")
  (#single-or-multi-line-delimiter! @argumentOrParameter @_dummy ", " ",\n")
)

;; !! void foo(int a, int b)
;; !           ^^^^^^^^^^^^
(_
  (parameter_list
    "(" @argumentList.removal.start.endOf @argumentOrParameter.iteration.start.endOf
    ")" @argumentList.removal.end.startOf @argumentOrParameter.iteration.end.startOf
  ) @argumentList
  (#empty-single-multi-delimiter! @argumentList @argumentList "" ", " ",\n")
  (#child-range! @argumentList 1 -2)
) @argumentList.domain @argumentOrParameter.iteration.domain

(parameter_list
  "(" @name.iteration.start.endOf @value.iteration.start.endOf @type.iteration.start.endOf
  ")" @name.iteration.end.startOf @value.iteration.end.startOf @type.iteration.end.startOf
)

;;!! int foo, bar;
;;!      ^^^  ^^^
(
  (variable_declaration
    type: (_)
    (variable_declarator)? @_.leading.endOf
    .
    (variable_declarator) @collectionItem
    .
    (variable_declarator)? @_.trailing.startOf
  ) @_dummy
  (#single-or-multi-line-delimiter! @collectionItem @_dummy ", " ",\n")
)

;;!! int foo, bar;
;;!      ^^^^^^^^
;;!  -------------
(_
  (variable_declaration
    type: (_)
    .
    (_) @collectionItem.iteration.start.startOf
  ) @collectionItem.iteration.end.endOf
) @collectionItem.iteration.domain

;;!! catch(Exception ex) {}
;;!        ^^^^^^^^^^^^
;;!        ^^^^^^^^^
;;!                  ^^
(catch_declaration
  type: (_) @argumentOrParameter.start @type @_.domain.start
  name: (_) @argumentOrParameter.end @name @_.domain.end
)

operator: [
  "->"
  "<"
  "<<"
  "<="
  ">"
  ">="
  ">>"
] @disqualifyDelimiter

(assignment_operator
  [
    "<<="
    ">>="
  ] @disqualifyDelimiter
)

(lambda_expression
  "=>" @disqualifyDelimiter
)

(member_access_expression
  "->" @disqualifyDelimiter
)
